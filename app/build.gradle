buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.deploygate:gradle:0.6'
        classpath 'me.champeau.gradle:gradle-groovy-android-plugin:0.3.4'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'me.champeau.gradle.groovy-android'

androidGroovy {
    options {
        sourceCompatibility = '1.7'
        targetCompatibility = '1.7'
    }
}

android {
    compileSdkVersion 21
    buildToolsVersion "21.0.2"

    def hasReleaseAuthority = new File("${rootDir}/gradle.properties").exists()
    if (hasReleaseAuthority) {
        signingConfigs {
            release {
                storeFile file(project.properties.storeFile)
                storePassword project.properties.storePassword
                keyAlias project.properties.storeAlias
                keyPassword project.properties.keyPassword
            }
        }
    }
    defaultConfig {
        applicationId "net.ichigotake.pyazing"
        minSdkVersion 16
        targetSdkVersion 21
        versionCode 8
        versionName "1.7"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (hasReleaseAuthority) {
                signingConfig signingConfigs.release
            }
            zipAlignEnabled true
            shrinkResources true
        }
    }
}

dependencies {
    compile 'com.deploygate:sdk:3.1'
    compile 'com.dmitriy-tarasov:android-intents:1.1.0'
    compile 'com.loopj.android:android-async-http:1.4.5'
    compile 'org.codehaus.groovy:groovy:2.4.0-beta-3:grooid'
}

apply plugin: 'deploygate'

// アップロード時のメッセージは /app/DEPLOYGATE を利用している
def hasDeployGateAuthority = new File("${rootDir}/gradle.properties").exists()
if (hasDeployGateAuthority) {
    deploygate {
        userName = project.properties.deployGateUserName
        token = project.properties.deployGateToken

        apks {
            release {
                sourceFile = file("build/outputs/apk/app-release.apk")
                message = file("DEPLOYGATE").readLines()[0]
            }
        }
    }
}
